20 DIM X1$(127)
30 X1$="(C) 1983 DATABAR CORP.SN09970008"
40 DIM R$(22)
50 REM MILES PER GALLON
60 REM 
70 REM 
80 REM COPYRIGHT 1983
90 REM DATABAR CORP.
100 REM EDEN PRAIRIE, MN  55344
110 REM 
120 REM 
130 REM *****  DICTIONARY  *****
140 REM *** X  = LOOP COUNTER
150 REM *** M1 = STARTING MILEAGE
160 REM *** M2 = ENDING MILEAGE
170 REM *** G  = GALLONS OF GAS USED
180 REM *** MG = MILES PER GALLON
190 REM 
200 REM ***** FORMULA *****
210 REM MG=INTEGER VALUE OF ((M1-M2)/G)
220 REM 
230 REM THE -DATA- STATEMENTS AT THE
240 REM END OF THE PROGRAM HAVE THE
250 REM MILEAGE AND GALLONS DATA TAKEN
260 REM DURING THE TRIP.  THE PROGRAM
270 REM WORKS BY GETTING A NEW MILEAGE
280 REM READING FROM THE -DATA- AND
290 REM SUBTRACTING IT FROM THE CURRENT
300 REM MILEAGE READING.  THIS GIVES
310 REM THE NUMBER OF MILES TRAVELED
320 REM BETWEEN FILL-UPS.  THE NUMBER
330 REM OF MILES IS DIVIDED BY THE
340 REM NUMBER OF GALLONS USED TO GET
350 REM THE MILES PER GALLON.  THIS
360 REM PROCESS IS REPEATED FOR ALL OF
370 REM THE MILEAGE READINGS.
380 REM 
390 REM SET STARTING MILEAGE AND PRINT
400 REM THE COLUMN HEADINGS.
410 REM 
420 LET M1=11406.4
430 PRINT "   TRIP TO PHOENIX"
440 PRINT 
450 PRINT "             # OF"
460 PRINT "MILEAGE     GALLONS  MPG"
470 REM 
480 REM THERE ARE 6 READINGS TO DO.
490 REM READ THE NEXT MILEAGE AND
500 REM GALLONS USED DATA.
510 REM 
520 FOR X=1 TO 6
530 READ M2,G
540 REM 
550 REM CALCULATE MILES PER GALLON AND
560 REM PRINT THE ANSWER.  THEN MAKE
570 REM THE NEW MILEAGE READING THE
580 REM STARTING MILEAGE FOR THE NEXT
590 REM CALCULATION.  DO THE LOOP
600 REM AGAIN.
610 REM 
620 LET MG=INT((M2-M1)/G)
630 PRINT M2;CHR$(127);G;CHR$(127);MG
640 LET M1=M2
650 NEXT X
660 END 
670 REM THE DATA FOR THE MILEAGE AND
680 REM GALLONS USED IS NEXT.
690 REM MILES-- GALS--MILES--GALS
700 DATA 11520.7,7.30,11678.6,9.80
710 DATA 11823.2,8.65,12118.4,20.7
720 DATA 12394.0,18.4,12657.7,16.2
730 REM WHAT LINE WOULD YOU ADD TO
740 REM MAKE THIS PROGRAM START OVER?
750 REM WE'LL TELL YOU HOW TO MAKE
760 REM PROGRAMS DO WHAT YOU WANT
770 REM IN THE NEXT FEW ISSUES.

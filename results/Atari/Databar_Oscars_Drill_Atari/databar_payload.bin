20 DIM X1$(127)
30 X1$="(C) 1983 DATABAR CORP.SN09970004"
40 DIM R$(22)
50 REM OSCAR'S DRILL
60 REM 
70 REM 
80 REM COPYRIGHT 1983
90 REM DATABAR CORP.
100 REM EDEN PRAIRIE, MN 55344
110 REM 
120 REM 
130 REM 
140 REM *****  DICTIONARY  *****
150 REM *** SC  = SCORE ON THE DRILL
160 REM *** A   = FIRST RANDOM NUMBER
170 REM *** B   = SECOND RANDOM NUMBER
180 REM *** S   = OSCAR'S NUMBER
190 REM *** P   = USED FOR DELAY
200 REM *** PN  = PROBLEM NUMBER
210 REM *** R$  = Y/N RESPONSE
220 REM 
230 REM START WITH 20 POINTS
240 LET SC=20
250 REM 
260 REM THE PROBLEM NUMBER SHOULD
270 REM COUNT FROM 1 TO 4 AUTOMATICALLY
280 REM 
290 FOR PN=1 TO 4
300 REM 
310 REM NOW SELECT TWO RANDOM NUMBERS
320 REM BETWEEN 0 AND 10
330 REM 
340 LET A=INT(11*RND(1))
350 LET B=INT(11*RND(1))
360 REM 
370 REM SHOW OSCAR THE PROBLEM
380 REM 
390 PRINT 
400 PRINT A;" X ";B;" = ";
410 REM 
420 REM GET OSCAR'S ANSWER
430 REM 
440 INPUT S
450 REM 
460 REM CHECK TO SEE IF HE WAS RIGHT OR
470 REM WRONG
480 REM 
490 IF S=A*B THEN 590
500 REM 
510 REM HE WAS WRONG, SHOW ANSWER
520 REM 
530 PRINT 
540 PRINT "SORRY, THAT'S ";A*B;"."
550 LET SC=SC-2
560 PRINT 
570 PRINT "YOUR SCORE IS ";SC;"."
580 GOTO 660
590 REM 
600 REM HE WAS RIGHT
610 REM 
620 PRINT 
630 PRINT "RIGHT!"
640 LET SC=SC+5
650 PRINT "YOUR SCORE IS ";SC;"."
660 REM 
670 REM GO ON TO THE NEXT PROBLEM
680 REM 
690 NEXT PN
700 REM 
710 REM AFTER 4 PROBLEMS ASK OSCAR IF
720 REM HE WANTS TO PLAY AGAIN.
730 REM 
740 PRINT 
750 PRINT "THE GAME IS OVER."
760 PRINT "YOUR SCORE WAS ";SC;"."
770 PRINT "WANT TO PLAY AGAIN?"
780 PRINT "TYPE Y FOR YES, N FOR NO.";
790 INPUT R$
800 IF R$="Y" THEN 240
810 PRINT 
820 PRINT "OK, COME BACK SOON!"
830 REM WHAT IS ALL THIS STUFF?
840 REM DON'T WORRY; WE'LL SOON SHOW
850 REM YOU HOW TO MAKE SENSE OF IT!
